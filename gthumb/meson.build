# Sources

public_header_files = [
  'cairo-scale.h',
  'cairo-utils.h',
  'color-utils.h',
  'dom.h',
  'gfixed.h',
  'gimp-op.h',
  'gio-utils.h',
  'glib-utils.h',
  'gnome-desktop-thumbnail.h',
  'gsignature.h',
  'gth-accel-button.h',
  'gth-accel-dialog.h',
  'gth-auto-paned.h',
  'gth-async-task.h',
  'gth-buffer-data.h',
  'gth-browser.h',
  'gth-color-manager.h',
  'gth-color-scale.h',
  'gth-delete-task.h',
  'gth-duplicable.h',
  'gth-empty-list.h',
  'gth-extensions.h',
  'gth-file-chooser-dialog.h',
  'gth-error.h',
  'gth-file-data.h',
  'gth-file-list.h',
  'gth-file-properties.h',
  'gth-file-selection.h',
  'gth-file-selection-info.h',
  'gth-file-source.h',
  'gth-file-source-vfs.h',
  'gth-file-store.h',
  'gth-file-tool.h',
  'gth-file-view.h',
  'gth-filter.h',
  'gth-filterbar.h',
  'gth-filter-editor-dialog.h',
  'gth-filter-file.h',
  'gth-filter-grid.h',
  'gth-folder-tree.h',
  'gth-grid-view.h',
  'gth-histogram.h',
  'gth-histogram-view.h',
  'gth-hook.h',
  'gth-icc-profile.h',
  'gth-icon-cache.h',
  'gth-image.h',
  'gth-image-dragger.h',
  'gth-image-history.h',
  'gth-image-list-task.h',
  'gth-image-loader.h',
  'gth-image-navigator.h',
  'gth-image-overview.h',
  'gth-image-preloader.h',
  'gth-image-saver.h',
  'gth-image-selector.h',
  'gth-image-task.h',
  'gth-image-task-chain.h',
  'gth-image-utils.h',
  'gth-image-viewer.h',
  'gth-image-viewer-tool.h',
  'gth-info-bar.h',
  'gth-load-file-data-task.h',
  'gth-location-bar.h',
  'gth-location-chooser.h',
  'gth-location-chooser-dialog.h',
  'gth-main.h',
  'gth-menu-manager.h',
  'gth-metadata.h',
  'gth-metadata-chooser.h',
  'gth-metadata-provider.h',
  'gth-monitor.h',
  'gth-overwrite-dialog.h',
  'gth-paned.h',
  'gth-preferences.h',
  'gth-progress-dialog.h',
  'gth-property-view.h',
  'gth-request-dialog.h',
  'gth-save-file-data-task.h',
  'gth-save-image-task.h',
  'gth-screensaver.h',
  'gth-shortcut.h',
  'gth-sidebar.h',
  'gth-sidebar-section.h',
  'gth-statusbar.h',
  'gth-string-list.h',
  'gth-tags-entry.h',
  'gth-tags-file.h',
  'gth-template-editor-dialog.h',
  'gth-template-selector.h',
  'gth-task.h',
  'gth-test.h',
  'gth-test-aspect-ratio.h',
  'gth-test-category.h',
  'gth-test-chain.h',
  'gth-test-selector.h',
  'gth-test-simple.h',
  'gth-thumb-loader.h',
  'gth-time.h',
  'gth-time-selector.h',
  'gth-toolbox.h',
  'gth-uri-list.h',
  'gth-user-dir.h',
  'gth-vfs-tree.h',
  'gth-viewer-page.h',
  'gth-window.h',
  'gtk-utils.h',
  'gvaluehash.h',
  'pixbuf-cache.h',
  'pixbuf-io.h',
  'pixbuf-utils.h',
  'str-utils.h',
  'typedefs.h',
  'uri-utils.h',
  'zlib-utils.h'
]

install_headers(
  public_header_files,
  install_dir : pkgincludedir
)

enum_files = gnome.mkenums_simple('gth-enum-types',
  sources: files(public_header_files),
  install_header : true,
  install_dir : pkgincludedir,
)

marshal_files = gnome.genmarshal('gth-marshal',
  prefix : 'gth_marshal',
  sources : 'gth-marshal.list',
  install_header : true,
  install_dir : pkgincludedir,
)

gthumb_h = custom_target('gthumb.h',
  input : [ 'gthumb.h.in' ],
  output : 'gthumb.h',
  command : [ find_program('make-gthumb-h.py'), '@INPUT@', '@OUTPUT@', 'gth-enum-types.h', 'gth-marshal.h', public_header_files ],
  install : true,
  install_dir : pkgincludedir
)

gthumb_h_dep = declare_dependency(sources : [ enum_files, marshal_files, gthumb_h ])

authors_tab = custom_target('authors.tab',
  input : join_paths(meson.source_root(), 'AUTHORS'),
  output : 'AUTHORS.tab',
  command : [ find_program('make-authors-tab.py'), '@INPUT@', '@OUTPUT@' ]
)
authors_dep = declare_dependency(sources : [ authors_tab ])

gresource_files = gnome.compile_resources('gth-resources', 'gthumb.gresource.xml', c_name : 'gth')

source_files = files(
  'cairo-scale.c',
  'cairo-utils.c',
  'color-utils.c',
  'dlg-favorite-properties.c',
  'dlg-location.c',
  'dlg-personalize-filters.c',
  'dlg-preferences.c',
  'dlg-preferences-browser.c',
  'dlg-preferences-extensions.c',
  'dlg-preferences-general.c',
  'dlg-preferences-shortcuts.c',
  'dlg-sort-order.c',
  'dom.c',
  'gimp-op.c',
  'gio-utils.c',
  'glib-utils.c',
  'gsignature.c',
  'gth-accel-button.c',
  'gth-accel-dialog.c',
  'gth-application.c',
  'gth-auto-paned.c',
  'gth-async-task.c',
  'gth-browser.c',
  'gth-browser-actions-callbacks.c',
  'gth-buffer-data.c',
  'gth-color-manager.c',
  'gth-color-scale.c',
  'gth-delete-task.c',
  'gth-duplicable.c',
  'gth-empty-list.c',
  'gth-error.c',
  'gth-extensions.c',
  'gth-file-chooser-dialog.c',
  'gth-file-comment.c',
  'gth-file-data.c',
  'gth-file-details.c',
  'gth-file-list.c',
  'gth-file-properties.c',
  'gth-file-selection.c',
  'gth-file-selection-info.c',
  'gth-file-source.c',
  'gth-file-source-vfs.c',
  'gth-file-store.c',
  'gth-file-tool.c',
  'gth-file-view.c',
  'gth-filter.c',
  'gth-filterbar.c',
  'gth-filter-editor-dialog.c',
  'gth-filter-file.c',
  'gth-filter-grid.c',
  'gth-folder-tree.c',
  'gth-grid-view.c',
  'gth-histogram.c',
  'gth-histogram-view.c',
  'gth-hook.c',
  'gth-icc-profile.c',
  'gth-icon-cache.c',
  'gth-image.c',
  'gth-image-dragger.c',
  'gth-image-history.c',
  'gth-image-list-task.c',
  'gth-image-loader.c',
  'gth-image-navigator.c',
  'gth-image-overview.c',
  'gth-image-preloader.c',
  'gth-image-saver.c',
  'gth-image-selector.c',
  'gth-image-task.c',
  'gth-image-task-chain.c',
  'gth-image-utils.c',
  'gth-image-viewer.c',
  'gth-image-viewer-tool.c',
  'gth-info-bar.c',
  'gth-load-file-data-task.c',
  'gth-location-bar.c',
  'gth-location-chooser.c',
  'gth-location-chooser-dialog.c',
  'gth-main.c',
  'gth-main-default-hooks.c',
  'gth-main-default-metadata.c',
  'gth-main-default-sort-types.c',
  'gth-main-default-tests.c',
  'gth-main-default-types.c',
  'gth-menu-manager.c',
  'gth-metadata.c',
  'gth-metadata-chooser.c',
  'gth-metadata-provider.c',
  'gth-metadata-provider-file.c',
  'gth-monitor.c',
  'gth-overwrite-dialog.c',
  'gth-paned.c',
  'gth-preferences.c',
  'gth-progress-dialog.c',
  'gth-property-view.c',
  'gth-request-dialog.c',
  'gth-save-file-data-task.c',
  'gth-save-image-task.c',
  'gth-screensaver.c',
  'gth-shortcut.c',
  'gth-shortcuts-window.c',
  'gth-sidebar.c',
  'gth-sidebar-section.c',
  'gth-statusbar.c',
  'gth-string-list.c',
  'gth-tags-entry.c',
  'gth-tags-file.c',
  'gth-template-editor-dialog.c',
  'gth-template-selector.c',
  'gth-task.c',
  'gth-test.c',
  'gth-test-aspect-ratio.c',
  'gth-test-category.c',
  'gth-test-chain.c',
  'gth-test-selector.c',
  'gth-test-simple.c',
  'gth-thumb-loader.c',
  'gth-time.c',
  'gth-time-selector.c',
  'gth-toolbox.c',
  'gth-trash-task.c',
  'gth-uri-list.c',
  'gth-user-dir.c',
  'gth-vfs-tree.c',
  'gth-viewer-page.c',
  'gth-window.c',
  'gth-window-title.c',
  'gtk-utils.c',
  'gvaluehash.c',
  'main.c',
  'main-migrate-catalogs.c',
  'pixbuf-cache.c',
  'pixbuf-io.c',
  'pixbuf-utils.c',
  'str-utils.c',
  'uri-utils.c',
  'zlib-utils.c'
)

external_files = files(
  'gnome-desktop-thumbnail.c',
  'gnome-thumbnail-pixbuf-utils.c'
)

gthumb_inc = include_directories('.')

# Build targets

gthumb_exe = executable('gthumb',
  sources : [
    config_file,
    marshal_files,
    enum_files,
    source_files,
    external_files,
    gresource_files
  ],
  dependencies : [
    common_deps,
    jpeg_deps,
    authors_dep,
    use_exiv2 ? exiv2_dep : [],
    use_clutter ? clutter_deps : [],
    use_gstreamer ? gstreamer_deps : [],
    use_libchamplain ? libchamplain_deps : [],
    use_lcms2 ? lcms2_dep : [],
    use_colord ? colord_dep : [],
    use_libtiff ? tiff_deps : [],
    use_libwebp ? libwebp_dep : [],
    use_libraw ? libraw_dep : [],
    use_librsvg ? librsvg_dep : [],
    with_webservices ? webkit2_dep : [],
    with_webservices ? libsoup_dep : [],
    with_webservices ? libjson_glib_dep : [],
    use_libsecret ? libsecret_dep : [],
    use_libbrasero ? libbrasero_dep : []
  ],
  include_directories : [ config_inc, gthumb_inc ],
  c_args : c_args,
  implib : true,
  install : true
)

# Tests

test('dom',
  executable('test-dom',
    sources : [ 'test-dom.c', 'dom.c'],
    dependencies : common_deps,
    include_directories : config_inc,
    c_args : c_args,
  )
)

test('glib-utils',
  executable('test-glib-utils',
    sources : [ 'test-glib-utils.c', 'glib-utils.c', 'str-utils.c', 'uri-utils.c' ],
    dependencies : common_deps,
    include_directories : config_inc,
    c_args : c_args,
  )
)

test('gtk-utils',
  executable('test-gtk-utils',
    sources : [ 'test-gtk-utils.c', 'gtk-utils.c', 'color-utils.c' ],
    dependencies : common_deps,
    include_directories : config_inc,
    c_args : c_args,
  )
)

test('gsignature',
  executable('test-gsignature',
    sources : [ 'test-gsignature.c', 'gsignature.c'],
    dependencies : common_deps,
    include_directories : config_inc,
    c_args : c_args,
  )
)

test('oauth',
  executable('test-oauth',
    sources : [ 'test-oauth.c', 'gsignature.c'],
    dependencies : common_deps,
    include_directories : config_inc,
    c_args : c_args,
  )
)
